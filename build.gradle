group 'uk.ac.ebi.subs'
version '0.3.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'application'

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}

repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
}

dependencies {
    compile("uk.ac.ebi.subs:subs-processing-model:0.1.1-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-repository:0.1.2-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-data-model:0.1.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-messaging:0.3.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:validator-common:0.5.0-SNAPSHOT")

    compile ('org.atteo:evo-inflector:1.2.1') // better pluralisation

    compile("org.springframework.boot:spring-boot-starter-data-mongodb")

    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    compile 'org.springframework.security:spring-security-core'
}

task sourceJar(type: Jar) { from sourceSets.main.allJava }

artifacts {
    archives jar
    archives sourceJar {
        classifier "sources" 
        destinationDir = new File("build/libs/sources")
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

            }

            pom.project {
                packaging 'jar'
                url 'https://github.com/EMBL-EBI-SUBS/subs-api-support'

                scm {
                    url 'https://github.com/EMBL-EBI-SUBS/subs-api-support.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'subs-internal'
                        name 'EMBL-EBI-SUBS'
                        email 'subs-internal@ebi.ac.uk'
                    }
                }
            }
        }
    }
}

ssh.settings {
    user = 'sub_adm'
}
 
if (project.hasProperty('sshKeyFile')) {
    ssh.settings.identity = new File(sshKeyFile)
}
 
if (project.hasProperty('sshPassphrase')) {
    ssh.settings.passphrase = sshPassphrase
}

task('externalCiTest', type: Test) {
    useJUnit {
        excludeCategories 'uk.ac.ebi.subs.apisupport.utils.MongoDBDependentTest'
    }
    testLogging {
        exceptionFormat = 'full'
    }
}

task printJarName {
    doLast {
        println "$jar.archivePath.name"
    }
 
}
 
task printJarFullPath {
    doLast {
        println "$jar.archivePath.absolutePath"
    }
}

ext.gradle_env = hasProperty('env') ? env : 'dev'

remotes {
    submission_dev {
        role('dev')
        host = 'mac-subs-001'
    }
    submission_test {
        role('test')
        host = 'mac-subs-002'
    }
}


task deployJar(type: SshTask, dependsOn: 'assemble') {
    doLast {
        File jarDeployDir = new File("/data/$gradle_env/$project.name/jar")
        sshRun(jarDeployDir)
    }
    ssh.settings {
        knownHosts = allowAnyHosts
    }
}

private Object sshRun(File jarDeployDir) {
    ssh.run {
        session(remotes.role(gradle_env)) {
            execute "/homes/sub_adm/create_dirs.sh $gradle_env $project.name", ignoreError: false
            println "Uploading jar $jar.archivePath.name to $jarDeployDir area on $remote"
            put(jar.archivePath.absolutePath, jarDeployDir.absolutePath)
            println "creating symbolic link to $jar.archiveName"
            def result = execute "ln -s -f $jarDeployDir/$jar.archiveName $jarDeployDir/$jar.baseName" + ".jar", ignoreError: true
            println result
            println "updating permissions of $jarDeployDir/$jar.archiveName"
            execute "chmod u+x $jarDeployDir/*", ignoreError: false
        }
    }
}

mainClassName = "uk.ac.ebi.subs.ApiSupportApplication"

springBoot {
    executable = true
}
